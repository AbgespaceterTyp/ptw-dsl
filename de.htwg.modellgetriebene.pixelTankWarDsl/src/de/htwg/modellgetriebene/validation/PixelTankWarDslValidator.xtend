/*
 * generated by Xtext 2.13.0
 */
package de.htwg.modellgetriebene.validation

import de.htwg.modellgetriebene.pixelTankWarDsl.ActionComponents
import de.htwg.modellgetriebene.pixelTankWarDsl.Battlefield
import de.htwg.modellgetriebene.pixelTankWarDsl.Block
import de.htwg.modellgetriebene.pixelTankWarDsl.PixelTankWarDslPackage
import de.htwg.modellgetriebene.pixelTankWarDsl.Player
import de.htwg.modellgetriebene.pixelTankWarDsl.impl.PixelTankWarDslPackageImpl
import org.eclipse.xtext.validation.Check

/**
 * This class contains custom validation rules. 
 *
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class PixelTankWarDslValidator extends AbstractPixelTankWarDslValidator {
	
	public static val DUPLICATE_NAME = 'duplicateName'
	public static val DUPLICATE_COLOR = 'duplicateColor'
	public static val HEALTH_POINTS_BELOW_ONE = 'healthPointsBelowOne'
	public static val HEALTH_POINTS_TOO_HIGH = 'healthPointsTooHigh'
	public static val ACTION_POINTS_BELOW_ONE = 'actionPointsBelowOne'
	public static val ACTION_POINTS_TOO_HIGH = 'actionPointsTooHigh'
	public static val LOCATION_OUT_OF_BOUNDS = 'locationOutOfBounds'
	public static val DUPLICATE_LOCATION_PLAYER = 'duplicateLocationPlayer'
	public static val DUPLICATE_LOCATION_BLOCK = 'duplicateLocationBlock'
	public static val BATTLEFIELD_TOO_SMALL = 'battleieldTooSmall'
	public static val PLAYER_DUPLICATE_ACTION = 'playerDuplicateAction'
	public static val DUPLICATE_ACTION = 'duplicateAction'

	private var Battlefield battlefield

	@Check
	def loadBattlefield(Battlefield bf) {
		battlefield = bf
	}
	
	@Check
	def checkBattlefieldDimensions(Battlefield bf) {
		if(battlefield.dimesion.height < 1 || battlefield.dimesion.width < 1) {
			error("Height and Width have to be greater than 0", PixelTankWarDslPackage.Literals.BATTLEFIELD__DIMESION, BATTLEFIELD_TOO_SMALL)
		}
	}

	@Check
	def checkPlayerNameIsUnique(Player player) {
		battlefield.players.players
			.groupBy[name]
			.forEach[p1, p2| 
			if(player.name.equals(p1) && p2.size() > 1) { 
				error("Name should be unique", PixelTankWarDslPackage.Literals.PLAYER__NAME, DUPLICATE_NAME)	
			}
		];
	}
	
	@Check
	def checkPlayerColorIsUnique(Player player) {
		battlefield.players.players
			.groupBy[color]
			.forEach[p1, p2| 
			if(player.color.equals(p1) && p2.size() > 1) { 
				error("Color should be unique", PixelTankWarDslPackage.Literals.PLAYER__COLOR, DUPLICATE_COLOR)	
			}
		];
	}

	@Check
	def checkPlayersHaveNoOccupiedPosition(Player player) {
		var playersLocations = battlefield.players.players.map[p | p.location]
		var blocksLocations = battlefield.blocks.blocks.map[b | b.location]
		
		var locations = playersLocations + blocksLocations
		
		var locationOccurrences = locations
			.filter[location | 
			player.location.XPosition === (location.XPosition) && player.location.YPosition === (location.YPosition)
			].size() 			
		
		if(locationOccurrences > 1) {
			error("Position should be unique", PixelTankWarDslPackage.Literals.PLAYER__LOCATION, DUPLICATE_LOCATION_PLAYER)		
		}
	}
	
	@Check
	def checkBlocksHaveNoOccupiedPosition(Block block) {
		var playersLocations = battlefield.players.players.map[p | p.location]
		var blocksLocations = battlefield.blocks.blocks.map[b | b.location]
		
		var locations = playersLocations + blocksLocations
		
		var locationOccurrences = locations
			.filter[location | 
			block.location.XPosition === (location.XPosition) && block.location.YPosition === (location.YPosition)
			].size()
		
		if(locationOccurrences > 1) {
			error("Position should be unique", PixelTankWarDslPackage.Literals.BLOCK__LOCATION, DUPLICATE_LOCATION_BLOCK)		
		}
	}

	@Check
	def checkPlayerHealthPointsAreInRange(Player player) {
		if(player.healthPoints < 1) {
			error("Health points must not be below 1", PixelTankWarDslPackage.Literals.PLAYER__HEALTH_POINTS, HEALTH_POINTS_BELOW_ONE)	
		}
		
		if(player.healthPoints > 10) {
			warning("Health points should be between 1 and 10, otherwise the game takes a long time to finish", PixelTankWarDslPackage.Literals.PLAYER__HEALTH_POINTS, HEALTH_POINTS_TOO_HIGH)
		}
	}
	
	@Check
	def checkPlayerActionPointsAreInRange(Player player) {
		if(player.actionPoints < 1) {
			error("Action points must not be below 1", PixelTankWarDslPackage.Literals.PLAYER__ACTION_POINTS, ACTION_POINTS_BELOW_ONE)
		}
		
		if(player.actionPoints > 10) {
			warning("Action points should be between 1 and 10, otherwise the game takes a long time to finish", PixelTankWarDslPackage.Literals.PLAYER__ACTION_POINTS, ACTION_POINTS_TOO_HIGH)	
		}
	}
	
	@Check
	def checkPlayerPositionsAreInRange(Player player) {
		if(player.location.XPosition > battlefield.dimesion.width) {
			error("X-Position has to within the battlefields width dimension", PixelTankWarDslPackage.Literals.PLAYER__LOCATION, LOCATION_OUT_OF_BOUNDS)
		}
		
		if(player.location.YPosition > battlefield.dimesion.height) {
			error("Y-Position has to be within the battlefields height dimension", PixelTankWarDslPackage.Literals.PLAYER__LOCATION, LOCATION_OUT_OF_BOUNDS)
		}
	}
	
	@Check
	def checkBlockPositionsAreInRange(Block block) {
		if(block.location.XPosition > battlefield.dimesion.width) {
			error("X-Position has to within the battlefields width dimension", PixelTankWarDslPackage.Literals.BLOCK__LOCATION, LOCATION_OUT_OF_BOUNDS)
		}
		
		if(block.location.YPosition > battlefield.dimesion.height) {
			error("Y-Position has to be within the battlefields height dimension", PixelTankWarDslPackage.Literals.BLOCK__LOCATION, LOCATION_OUT_OF_BOUNDS)
		}
	}
	
	@Check
	def checkPlayerHasNoDuplicateActions(Player player) {
		val epackage = EPackages.get(0) as PixelTankWarDslPackageImpl
		
		epackage
			.actionType
			.ELiterals
			.forEach[actionType | { 
				if(player.playerActions.playerActions.filter[action | action.name() === actionType.name].size() > 1) {
					error("An action has to be assigned only once", PixelTankWarDslPackage.Literals.PLAYER__PLAYER_ACTIONS, PLAYER_DUPLICATE_ACTION)
				}
			}]
	}
	
	@Check
	def checkNoDuplicateDefinedActions(ActionComponents actionComponents) {
		if(battlefield.action.action.filter[action | action.actionType.name() === actionComponents.actionType.name()].size() > 1) {
			error("An action has to be defined only once", PixelTankWarDslPackage.Literals.ACTION_COMPONENTS__ACTION_TYPE, DUPLICATE_ACTION)
		}
	}	
}
