grammar de.htwg.modellgetriebene.PixelTankWarDsl with org.eclipse.xtext.common.Terminals

generate pixelTankWarDsl "http://www.htwg.de/modellgetriebene/PixelTankWarDsl"

Model:
	// Ohne Stern, da nur ein Battlefield
	game=Game;
	
Game:
	'Create' Battlefield;
	
Battlefield:
	'Battlefield' battlefield=ID 
		'withSize' dimesion=Dimension
		'andBackground' background=Background 
		'itHas' players=Players blocks=Blocks
		'theActionsAre' action=Action
;


// Battlefield attributes

Location: xPosition=INT ',' yPosition=INT;

Dimension:
	(width=INT'x'height=INT);

enum Direction: NORTH | EAST | SOUTH | WEST;

enum Color: 
	RED | BLUE | PURPLE | BROWN;

enum Background:
	FORREST | DESERT | MOUNTAINS;
	

// Player

Players:
	('aPlayer' players+=Player) &
	('aPlayer' players+=Player) & 
	('aPlayer' players+=Player)? &
	('aPlayer' players+=Player)?
;

Player: 
		'named' name=ID
		'withColor' color=Color
		'andForce' actionPoints=INT
		'andHealth' healthPoints=INT
		'whoStartsAtPosition' location=Location
		'towards' startDirection=Direction
		'withActions' playerActions=PlayerActions
;

PlayerActions: {PlayerActions} (playerActions+=ActionType)*;


// Blocks
	
Blocks: {Blocks} ('aBlock' blocks+=Block)*;

Block: 'ofType' blockType=BlockType 'isAtPosition' location=Location;

enum BlockType: WOOD | MOUNTAIN | LAKE | CITY | CITY_DESERT | HELICOPTER | DUNE | PALM;


// Actions

Action: {Action} (action+=ActionComponents)*;

ActionComponents: actionType=ActionType 'withCosts' costs=INT 'andRange' range=INT 'andDamage' damage=INT;
	
enum ActionType: MOVE | SHOOT | ROCKET | WAIT;